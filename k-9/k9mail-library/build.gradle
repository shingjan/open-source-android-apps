apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

if (!rootProject.optimizeForDevelopment) {
    apply from: '../gradle/plugins/checkstyle-android.gradle'
    apply from: '../gradle/plugins/findbugs-android.gradle'
}

if (rootProject.testCoverage) {
    apply plugin: 'jacoco'
}

dependencies {
    implementation 'org.apache.james:apache-mime4j-core:0.8.1'
    implementation 'org.apache.james:apache-mime4j-dom:0.8.1'
    implementation "com.squareup.okio:okio:${okioVersion}"
    implementation 'commons-io:commons-io:2.4'
    implementation 'com.jcraft:jzlib:1.0.7'
    implementation 'com.beetstra.jutf7:jutf7:1.0.0'
    implementation "com.android.support:support-annotations:${androidSupportLibraryVersion}"
    implementation "com.jakewharton.timber:timber:${timberVersion}"

    androidTestImplementation 'com.android.support.test:runner:0.4.1'
    androidTestImplementation 'com.madgag.spongycastle:pg:1.51.0.0'

    testImplementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:${kotlinVersion}"
    testImplementation "org.robolectric:robolectric:${robolectricVersion}"
    testImplementation "junit:junit:${junitVersion}"
    testImplementation "com.google.truth:truth:${truthVersion}"
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"

    // The Android Gradle plugin doesn't seem to put the Apache HTTP Client on the runtime classpath anymore when
    // running JVM tests.
    testImplementation 'org.apache.httpcomponents:httpclient:4.5.5'
}

android {
    compileSdkVersion androidCompileSdkVersion.toInteger()
    buildToolsVersion androidBuildToolsVersion

    defaultConfig {
        minSdkVersion 15

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    // for using Apache HTTP Client
    useLibrary 'org.apache.http.legacy'

    buildTypes {
        debug {
            testCoverageEnabled rootProject.testCoverage
        }
    }

    lintOptions {
        abortOnError false
        lintConfig file("$rootProject.projectDir/config/lint/lint.xml")
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'LICENSE.txt'
    }
}
