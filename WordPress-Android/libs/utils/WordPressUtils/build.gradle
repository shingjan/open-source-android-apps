buildscript {
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'
        classpath 'com.novoda:bintray-release:0.5.0'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'com.novoda.bintray-release'

repositories {
    google()
    jcenter()
}

dependencies {
    implementation 'org.apache.commons:commons-text:1.1'
    implementation 'com.mcxiaoke.volley:library:1.0.18'
    implementation 'com.android.support:support-v13:27.1.0'
    implementation 'com.android.support:design:27.1.0'
    implementation 'com.android.support:recyclerview-v7:27.1.0'
    implementation 'org.greenrobot:eventbus:3.0.0'

    lintChecks 'org.wordpress:lint:1.0.0'
}

android {
    useLibrary 'org.apache.http.legacy'

    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    defaultConfig {
        versionName "1.20.0"
        minSdkVersion 15
        targetSdkVersion 25
    }
}

android.libraryVariants.all { variant ->
    task("generate${variant.name}Javadoc", type: Javadoc) {
        description "Generates Javadoc for $variant.name."
        source = variant.javaCompile.source
        classpath = files(variant.javaCompile.classpath.files, android.getBootClasspath())

        options {
            links "http://docs.oracle.com/javase/7/docs/api/"
        }
        exclude '**/R.java'
    }
}

publish {
    artifactId = 'utils'
    userOrg = 'wordpress-mobile'
    groupId = 'org.wordpress'
    uploadName = 'utils'
    description = 'Utils library for Android'
    publishVersion = android.defaultConfig.versionName
    licences = ['MIT', 'GPL']
    website = 'https://github.com/wordpress-mobile/WordPress-Utils-Android/'
    dryRun = 'false'
    autoPublish = 'true'
}
